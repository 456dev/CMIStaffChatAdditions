plugins {
    id 'java'
    id("xyz.jpenilla.run-paper") version "2.0.0"
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group = 'dev.the456gamer'
version = '1.0.0'


repositories {
    mavenCentral()
    maven {
        name = 'papermc-repo'
        url = 'https://repo.papermc.io/repository/maven-public/'
    }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven { url 'https://nexus.scarsz.me/content/groups/public/' }
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
}




dependencies {
    compileOnly 'io.papermc.paper:paper-api:1.19.4-R0.1-SNAPSHOT'
    compileOnly(files("localdepends/CMI.jar", "localdepends/CMILib.jar")) // jitpack build is broken atm, download it yourself
    compileOnly("com.discordsrv:discordsrv:1.26.3-SNAPSHOT") // tmp until next (shaded) version is fully released
    implementation("org.apache.commons:commons-text:1.10.0")
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks {
    runServer {
        minecraftVersion("1.19.4")
    }
}

shadowJar {
    relocate 'org.apache.commons', 'dev.the456gamer.shadow.apachecommons'
}
